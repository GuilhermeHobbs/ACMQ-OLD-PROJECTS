10 '*********************************
11 '* PROJETO DE FILTROS PASSIVOS   *
12 '* Eduardo Rombom Halfen         *
13 '* Projeto final: DEEL/UFRJ 1987 *
14 '* Adaptado por ACMQ             *
15 '* Versao 1.1 de 01/12/87        *
16 '*********************************
17 DEFDBL A-Z
50 GOSUB 320
60 GOSUB 470
70 IF I4=0 THEN GOSUB 1160
80 IF I2>2 THEN OS=(WS-WP)/(W2-W1) ELSE OS=WS/WP
100 IF I3=1 THEN GOSUB 1830
110 IF I3>1 AND I3<4 THEN GOSUB 2200
120 IF I3=2 THEN GOSUB 2370
130 IF I3=3 THEN GOSUB 3020
140 IF I3=4 THEN GOSUB 3510
160 IF I1=1 THEN GOTO 240
170 PRINT:PRINT "Tocai uma tecla"
180 GOSUB 9030
190 IF I3=1 THEN GOSUB 6090
200 IF I3=2 THEN GOSUB 6230
210 IF I3=3 AND K=N/2 THEN GOSUB 3270
220 IF I3>2 THEN GOSUB 6430
240 PRINT:PRINT"Repetir p/outras especificacoes? (s/n)"
250 GOSUB 8910
260 IF A$<>"S" AND A$<>"N" THEN GOSUB 9000: GOTO 260
270 IF A$="S" THEN RUN 
280 END
320 PI=3.14159265358979#
330 DEF FNCH(X)=(EXP(X)+EXP(-X))/2
340 DEF FNSH(X)=(EXP(X)-EXP(-X))/2
350 DEF FNAS(X)=LOG(X+SQR(X*X+1))
360 DEF FNAC(X)=LOG(X+SQR(X*X-1))
370 DIM T(2),B$(2),TL(2),CI(2),CT(2),VT(2)
380 DIM MV(2,2),DV(2,2),MT(2),DT(2)
390 DIM A(3,24),B(3,24),X(3,24)
400 DIM O(24),V(24),R(2),C(24),L(24),CS(24),LS(24)
410 DIM F(24),Y(24),H(24),K(24),ZN(24),ZD(24)
420 H=1
430 RETURN
470 KEY OFF: CLS
471 PRINT"Sintese de filtros passivos"
472 PRINT"---------------------------"
473 PRINT"Eduardo Rombom Halfen - Versao 1.1"
490 PRINT:PRINT"O que desejais obter?"
500 PRINT:PRINT"1-Funcao de transducao"
510 PRINT"2-Sintese do filtro passivo"
520 GOSUB 8910 
530 IF VAL(A$)<1 OR VAL(A$)>2 THEN GOSUB 9000: GOTO 530
540 I1=VAL(A$) 
550 PRINT:PRINT"Que tipo de filtro desejais?"
560 PRINT:PRINT"1-Passa-baixas"
570 PRINT"2-Passa-altas"
580 PRINT"3-Passa-faixa"
590 PRINT"4-Rejeita-faixa"
600 GOSUB 8910
610 IF VAL(A$)<1 OR VAL(A$)>4 THEN GOSUB 9000: GOTO 610
620 I2=VAL(A$)
630 BP$="b. passante"
640 BR$="b. de rejeicao"
660 PRINT:PRINT"At. min. (dB) na ";BR$;
670 INPUT AZ
680 PRINT:PRINT"At. max. (dB) na ";BP$;
690 INPUT AP
700 IF AP>AZ THEN PRINT:PRINT"A atenuacao na b. p. nao pode ser maior que a na b. r.": GOTO 660
710 IF I2=1 OR I2=2 THEN B1$="superior": B2$="inferior"
720 IF I2=3 THEN B1$="posterior a "+BP$: B2$="anterior a "+BP$
730 IF I2=4 THEN B1$="posterior a "+BR$: B2$="anterior a "+BR$
750 PRINT:PRINT"Freq. de corte (Rad/s) ";B1$;
760 INPUT WS
770 PRINT:PRINT"Freq. de corte (Rad/s) ";B2$;
780 INPUT WP
790 IF WP>WS THEN PRINT"A frequencia minima nao pode ser maior que a maxima.": GOTO 750
800 IF I2>2 THEN GOSUB 970
810 PRINT:PRINT"Que tipo de aproximacao desejais?"
820 PRINT:PRINT"1-Butterworth"
830 PRINT"2-Chebyschev ou Chebyschev modificado"
840 PRINT"3-Chebyschev inverso"
850 PRINT"4-Eliptica ou eliptica modificada"
860 GOSUB 8910
870 IF VAL(A$)<1 OR VAL(A$)>4 THEN GOSUB 9000: GOTO 870
880 I3=VAL(A$)
890 PRINT:PRINT"Desejais overdesign (s/n)"
900 GOSUB 8910
910 IF A$<>"S" AND A$<>"N" THEN GOSUB 9000: GOTO 910
920 IF A$="N" THEN I4=1 ELSE I4=0
930 RETURN
970 IF I2=3 THEN B1$=BP$ ELSE B1$=BR$
990 PRINT:PRINT"Freq. maxima (Rad/s) da ";B1$;
1000 INPUT W2
1010 PRINT:PRINT"Freq. minima (Rad/s) da ";B1$;
1020 INPUT W1
1030 IF W1>W2 THEN PRINT"A freq. minima nao pode ser maior que a maxima.": GOTO 990
1070 WF=W2*W1
1080 W3=WP
1090 W4=WS
1100 IF WF>W3*W4 THEN WP=WF/W4 ELSE WS=WF/W3
1110 IF I2=3 THEN BF=W2-W1 ELSE BF=WS-WP
1120 RETURN
1160 PRINT:PRINT"Sera realizado overdesign para um:"
1170 PRINT:PRINT"1-Filtro ativo"
1180 PRINT"2-Filtro passivo"
1190 GOSUB 8910
1200 IF VAL(A$)<1 OR VAL(A$)>2 THEN GOSUB 9000: GOTO 1200
1210 I5=VAL(A$)
1220 PRINT:PRINT"Temperatura maxima"
1230 INPUT T(2)
1240 PRINT:PRINT"Temperatura minima"
1250 INPUT T(1)
1260 IF I5=1 THEN B$(1)="resistores" ELSE B$(1)="indutores"
1270 B$(2)="capacitores"
1280 FOR L=1 TO 2
1290 PRINT:PRINT"Tolerancia (%) dos ";B$(L);
1300 INPUT TL(L)
1310 PRINT:PRINT"Coeficiente de idade (%) dos ";B$(L);
1320 INPUT CI(L)
1330 PRINT:PRINT"Magnitude do coeficiente de temperatura (PPM/'C) dos ";B$(L);
1340 INPUT CT(L)
1350 PRINT:PRINT"Variacao do coeficiente de temperatura (PPM/'C) dos ";B$(L);
1360 INPUT VT(L)
1370 NEXT L
1380 FOR L=1 TO 2
1390 FOR M=1 TO 2
1400 MV(L,M)=CT(L)*(1E-06)*(T(M)-25)
1410 DV(L,M)=SQR((TL(L)/100)^2+(VT(L)*(1E-06)*(T(M)-25))^2+(CI(L)/100)^2)
1420 NEXT M
1430 NEXT L
1440 FOR M=1 TO 2
1450 MT(M)=MV(1,M)+MV(2,M)
1460 DT(M)=SQR(DV(1,M)^2+DV(2,M)^2)
1470 IF I5=2 THEN MT(M)=MT(M)/2: DT(M)=DT(M)/2
1480 NEXT M
1490 IF MT(2)>MT(1) THEN MM=MT(2): MN=MT(1): GOTO 1530
1500 MM=MT(1)
1510 MN=MT(2)
1530 IF DT(2)>DT(1) THEN DM=DT(2): DN=DT(1): GOTO 1570
1540 DM=DT(1)
1550 DN=DT(2)
1570 MI=MN-DM
1580 MX=MM+DN
1600 AZ=AZ+2*AP
1610 IF I2>2 THEN GOSUB 1660 ELSE GOSUB 1760
1620 RETURN
1660 WP=W3*(1+MX)
1670 WS=W4*(1+MI)
1680 W1=W1*(1+MI)
1690 W2=W2*(1+MX)
1700 GOSUB 1070
1710 IF W1>W2 OR WP>WS THEN PRINT"Erro: as bandas se superpoem": GOTO 240
1720 RETURN
1760 WP=WP*(1+MX)
1770 WS=WS*(1+MI)
1780 IF WP>WS THEN PRINT"Erro: as bandas se superpoem.": GOTO 240
1790 RETURN
1830 E=SQR(10^(AP/10)-1)
1840 N=(LOG(SQR(10^(AZ/10)-1)/E))/(LOG(OS))
1850 N=INT(N)+1
1860 IF I4=1 THEN GOTO 1900
1870 E=SQR(((10^(AZ/10))-1)/(OS^(2*N)))
1880 AP=10*LOG(1+E^2)/LOG(10)
1900 CLS
1910 PRINT"Aproximacao de Butterworth:"
1920 GOSUB 8770
1930 K=INT(N/2)
1940 FOR L=1 TO K+1
1950 B(0,L)=1
1960 B(1,L)=-2*COS((PI/2)*(2*L+N-1)/N)
2030 IF I1=1 AND I2=1 THEN GOSUB 5650
2040 IF I1=1 AND I2=2 THEN GOSUB 5810
2050 IF I1=1 AND I2=3 THEN GOSUB 5900
2060 IF I1=1 AND I2=4 THEN GOSUB 6010
2070 NEXT L
2080 IF I1=1 AND I2=1 THEN H=E/(WP^N)
2090 IF I1=1 AND I2=3 THEN H=BF^(-N)
2100 GOSUB 4860
2110 IF I1=1 AND I2>2 THEN GOSUB 5150
2120 IF I1=1 AND I2=3 THEN GOSUB 5250
2130 IF I1=1 AND I2=4 THEN GOSUB 5300
2140 IF I2<3 OR I1=2 THEN GOSUB 4930
2150 IF I1=1 AND I2=2 THEN GOSUB 5250
2160 RETURN
2200 E=SQR(10^(AP/10)-1)
2210 N=(AZ+(20*LOG(1/E))/LOG(10)+6)/(8.68*FNAC(OS))
2220 N=INT(N)+1
2230 CN=N*FNAC(OS)
2240 CN=FNCH(CN)
2250 IF I4=1 THEN GOTO 2290
2260 E=SQR((10^(AZ/10)-1)/(CN^2))
2270 AP=10*LOG(1+E^2)/LOG(10)
2290 CLS
2300 IF I3=3 THEN B2$=" inversa" ELSE B2$=""
2310 PRINT"Aproximacao de Chebyschev";B2$;":"
2320 GOSUB 8770
2330 RETURN
2370 K=INT(N/2)
2380 FOR L=1 TO K+1
2390 GOSUB 3440
2400 B(0,L)=RE^2+IM^2
2410 AH=B(0,L)
2420 H=H*B(0,L)
2430 B(1,L)=-2*RE
2440 IF I1=1 AND I2=1 THEN GOSUB 5730
2450 IF I1=1 AND I2=2 THEN GOSUB 5810
2460 IF I1=1 AND I2=3 THEN GOSUB 5900
2470 IF I1=1 AND I2=4 THEN GOSUB 6010
2480 NEXT L
2490 IF K=N/2 THEN H=AH/H ELSE H= SQR(AH)/H
2500 IF I1=1 AND I2=1 THEN H=H/(WP^N)
2510 IF I1=1 AND(I2=2 OR I2=4) THEN H=1
2520 IF I1=1 AND I2=3 THEN H=H/(BF^N)
2530 GOSUB 4860
2540 IF I1=1 AND I2>2 THEN GOSUB 5150
2550 IF I2<3 OR I1=2 THEN GOSUB 4930
2560 IF I1=1 AND(I2=2 OR I2=3) THEN GOSUB 5250
2570 IF I1=1 AND I2=4 THEN GOSUB 5300
2580 IF K<>N/2 THEN GOTO 2880
2590 PRINT:PRINT"Tocai uma tecla para a realizacao do filtro Chebyschev modificado"
2600 GOSUB 9030
2610 CLS
2620 PRINT"Aproximacao de Chebyschev modificada:"
2630 GOSUB 8770
2640 H=1
2650 OX=COS(PI*(N-1)/(2*N))
2660 FOR L=1 TO K
2670 GOSUB 3440
2680 GOSUB 2920
2690 B(0,L)=RE^2+IM^2
2700 H=H*B(0,L)
2710 B(1,L)=-2*RE
2720 IF I1=1 AND I2=1 THEN GOSUB 5730
2730 IF I1=1 AND I2=2 THEN GOSUB 5810
2740 IF I1=1 AND I2=3 THEN GOSUB 5900
2750 IF I1=1 AND I2=4 THEN GOSUB 6010
2760 NEXT L
2770 H=1/H
2780 IF I1=1 AND I2=1 THEN H=H/(WP^N)
2790 IF I1=1 AND(I2=2 OR I2=4) THEN H=1
2800 IF I1=1 AND I2=3 THEN H=H/(BF^N)
2810 GOSUB 4860
2820 IF I1=1 AND I2>2 THEN GOSUB 5150
2830 IF I1=1 AND I2=3 THEN GOSUB 5250
2840 IF I1=1 AND I2=4 THEN GOSUB 5300
2850 IF I2<3 OR I1=2 THEN GOSUB 4930
2860 IF I1=1 AND I2=2 THEN GOSUB 5250
2880 RETURN
2920 X=((RE^2)+(OX^2)-(IM^2))/(1-(OX^2)) 
2930 Y=(2*RE*IM)/(1-(OX^2)) 
2940 MO=SQR(X^2+Y^2) 
2950 BT=(MO-X)/2
2960 IM=SQR(BT)
2970 RE=Y/(2*IM) 
2980 RETURN
3020 E=1/(E*CN) 
3030 K=INT(N/2)
3040 FOR L=1 TO K+1
3050 IF L=K+1 THEN A(0,L)=1 ELSE A(0,L)=(COS(((2*L-1)*PI)/(2*N)))^-2
3060 GOSUB 3440 
3070 B(0,L)=1/(RE^2+IM^2)
3080 HH=A(0,L) 
3090 AH=B(0,L)/HH 
3100 H=H*B(0,L)/A(0,L)
3110 B(1,L)=-2*RE*B(0,L)
3120 GOSUB 5570
3130 NEXT L
3140 IF K=N/2 THEN H=AH/H ELSE H=SQR(AH/HH)/H
3150 IF I1=2 THEN GOTO 3200
3160 IF I2=1 AND K<>N/2 THEN H=H/WP
3170 IF I2=2 OR I2=4 THEN H=1
3180 IF I2=3 AND K<>N/2 THEN H=H/BF
3200 GOSUB 4860
3210 IF I1=1 AND I2>2 THEN GOSUB 5150 :GOSUB 5350
3220 IF I2<3 OR I1=2 THEN GOSUB 4930 :GOSUB 5030
3230 RETURN
3270 CLS
3280 PRINT"A aproximacao de Chebyschev inversa de ordem ";N;" nao e realizavel em ladder LCDT."
3290 N=N+1
3300 PRINT"A ordem do filtro sera alterada para um valor impar: ";N
3310 PRINT:PRINT"Tocai uma tecla"
3320 GOSUB 9030
3330 E=SQR(10^(AP/10)-1)
3340 CN=N*FNAC(OS)
3350 CN=FNCH(CN)
3360 GOSUB 2290
3370 GOSUB 3020
3380 PRINT:PRINT"Tocai uma tecla"
3390 GOSUB 9030
3400 RETURN
3440 AR=FNAS(1/E)
3450 RE=-(SIN((2*L-1)*PI/(2*N)))*(FNSH((1/N)*AR))
3460 IM=(COS((2*L-1)*PI/(2*N)))*(FNCH((1/N)*AR))
3470 RETURN
3510 Q=SQR(1-(1/OS)^2) 
3520 Q=.5*(1-SQR(Q))/(1+SQR(Q)) 
3530 Q=Q+(2*Q^5)+(15*Q^9)+(150*Q^13)
3540 D=(10^(AZ/10)-1)/(10^(AP/10)-1)
3550 N=LOG(16*D)/LOG(1/Q) 
3560 N=INT(N)+1
3570 IF I4=1 THEN GOTO 3610 
3580 D=(10^(N*LOG(1/Q)/LOG(10)))/16
3590 AP=10*LOG((D-1+10^(AZ/10))/D)/LOG(10)
3610 CLS
3620 PRINT"Aproximacao eliptica:"
3630 GOSUB 8770 
3640 A=(1/(2*N))*LOG((10^(.05*AP)+1)/(10^(.05*AP)-1)) 
3650 SB=FNSH(A) 
3660 SC=0 
3670 L=1
3690 B=((-1)^L)*(Q^(L^2+L))*FNSH((2*L+1)*A) 
3700 C=((-1)^L)*(Q^(L^2))*FNCH(2*L*A)
3710 SB=B+SB 
3720 SC=C+SC 
3730 R0=ABS((2*(Q^.25)*SB)/(1+(2*SC))) 
3740 IF R0=RR THEN GOTO 3790 
3750 RR=R0
3760 L=L+1
3770 GOTO 3690 
3790 W=SQR((1+(R0^2)/OS)*(1+(R0^2)*OS)) 
3800 K=INT(N/2)
3810 FOR L=1 TO K+1
3820 M=1 
3830 IF K=N/2 THEN ML=L-.5 ELSE ML=L 
3840 SB=SIN(PI*ML/N) 
3850 SC=0
3870 B=((-1)^M)*(Q^(M^2+M))*SIN((2*M+1)*PI*ML/N) 
3880 C=((-1)^M)*(Q^(M^2))*COS(2*M*PI*ML/N) 
3890 SB=B+SB
3900 SC=C+SC
3910 O(L)=(2*(Q^.25)*SB)/(1+(2*SC)) 
3920 IF O(L)=OP THEN GOTO 3970 
3930 OP=O(L) 
3940 M=M+1
3950 GOTO 3870
3970 V(L)=SQR((1-(O(L)^2)/OS)*(1-(O(L)^2)*OS)) 
3980 GOSUB 4790 
3990 H=H*B(0,L)/A(0,L)
4000 GOSUB 5570 
4010 NEXT L
4020 L=K+1
4030 H=H/B(0,L)
4040 B(0,L)=R0^2*OS 
4050 H=H*B(0,L)
4060 HH=A(0,L)
4070 AH=B(0,L)/HH
4080 GOSUB 5570 
4090 IF K=N/2 THEN H=AH/H ELSE H=SQR(AH/HH)/H
4100 IF I1=2 THEN GOTO 4150
4110 IF I2=1 AND K<>N/2 THEN H=H/WP
4120 IF I2=2 OR I2=4 THEN H=1
4130 IF I2=3 AND K<>N/2 THEN H=H/BF
4150 GOSUB 4860
4160 IF I1=1 AND I2>2 THEN GOSUB 5150 :GOSUB 5350
4170 IF I2<3 OR I1=2 THEN GOSUB 4930 :GOSUB 5030
4180 FOR L=1 TO K
4190 X(1,L)=OS*(O(L)^2)
4200 NEXT L
4210 IF K<>N/2 THEN GOTO 4750
4220 PRINT:PRINT"Tocai uma tecla para a realizacao do filtro eliptico modificado"
4230 GOSUB 9030
4240 CLS
4250 PRINT"Aproximacao eliptica modificada:"
4260 GOSUB 8770
4270 FOR L=1 TO K
4280 GOSUB 4790
4290 NEXT L
4310 OX=SQR(OS*O(1)^2)
4320 FOR L=1 TO K
4330 RE=-B(1,L)/2
4340 IM=.5*SQR( ABS(4*B(0,L)-B(1,L)^2))
4350 GOSUB 2920
4360 B(0,L)=RE^2+IM^2
4370 B(1,L)=2*RE
4380 A(0,L)=(A(0,L)-OX^2)/(1-OX^2)
4390 X(1,L)=(X(1,L)-OX^2)/(1-OX^2)
4400 NEXT L
4410 OX=SQR(1/A(0,1))
4420 FOR L=1 TO K
4430 RE=-B(1,L)/2
4440 IM=.5*SQR( ABS(4*B(0,L)-B(1,L)^2))
4450 GOSUB 2920
4460 B(0,L)=RE^2+IM^2
4470 B(1,L)=2*RE
4480 X=X(1,L)
4490 X(1,L)=((1/A(0,L))-OX^2)/(1-OX^2)
4500 IF X=0 THEN A(0,L)=0 ELSE A(0,L)=((1/X)-OX^2)/(1-OX^2) 
4510 X=X(1,L)
4520 IF A(0,L)=0 THEN X(1,L)=0 ELSE X(1,L)=1/A(0,L) 
4530 IF X=0 THEN A(0,L-1)=0 ELSE A(0,L-1)=1/X
4540 NEXT L
4560 H=1
4570 A(0,K)=1
4580 FOR L=1 TO K
4590 H=H*A(0,L)/B(0,L)
4600 GOSUB 5570 
4610 NEXT L
4620 IF I1=2 THEN GOTO 4670 
4630 IF I2=1 THEN H=H/(WP^2)
4640 IF I2=2 OR I2=4 THEN H=1
4650 IF I2=3 THEN H=H/(BF^2)
4670 GOSUB 4860 
4680 IF I1=1 AND I2>2 THEN GOSUB 5150 
4690 IF I2<3 OR I1=2 THEN GOSUB 4930
4700 K=K-1
4710 IF I1=1 AND I2>2 THEN GOSUB 5460
4720 IF I2<3 OR I1=2 THEN GOSUB 5030 
4730 K=K+1 
4750 RETURN
4790 A(0,L)=OS/(O(L)^2)
4800 B(0,L)=OS*((R0*V(L))^2+(O(L)*W)^2)/(1+(R0^2)*(O(L)^2))^2
4810 B(1,L)=SQR(OS)*(2*R0*V(L))/(1+(R0^2)*(O(L)^2))
4820 RETURN
4860 IF I1=1 THEN B2$="desnormalizada" ELSE B2$="normalizada"
4870 PRINT:PRINT"Funcao de transducao ";B2$;": H(S)=N(S)/D(S) "
4880 PRINT"N(S)=";H;
4890 RETURN
4930 FOR L=1 TO K
4940 PRINT"(S^2+";B(1,L);"S+";B(0,L);")";
4950 NEXT L
4960 IF K=N/2 THEN PRINT"" :RETURN
4970 B(0,K+1)=SQR(B(0,K+1))
4980 PRINT"(S+";B(0,K+1);")"
4990 RETURN
5030 PRINT:PRINT"D(S)=";
5040 IF K=N/2 THEN GOTO 5070 
5050 IF I1=1 AND I2=2 THEN PRINT"S";
5070 FOR L=1 TO K
5080 PRINT"(S^2+";A(0,L);")";
5090 NEXT L
5100 PRINT""
5110 RETURN
5150 FOR L=1 TO K
5160 PRINT"(S^4+";B(3,L);"S^3+";B(2,L);"S^2+";B(1,L);"S+";B(0,L);")";
5170 NEXT L
5180 IF K=N/2 THEN PRINT"": RETURN
5190 B(1,K+1)=SQR(B(2,K+1)-2*WF)
5200 PRINT"(S^2+";B(1,K+1);"S+";WF;")"
5210 RETURN
5250 PRINT:PRINT"D(S)=S^";N
5260 RETURN
5300 PRINT:PRINT"D(S)=(S^2+";WF;")^";N
5310 RETURN
5350 PRINT:PRINT"D(S)=";
5360 IF K<>N/2 AND I2=3 THEN PRINT"S";
5370 IF K<>N/2 AND I2=4 THEN PRINT"(S^2+";WF;")";
5380 FOR L=1 TO K
5390 PRINT"(S^4+";A(2,L);"S^2+";A(0,L);")";
5400 NEXT L
5410 PRINT""
5420 RETURN
5460 PRINT:PRINT"D(S)=";
5470 IF I2=3 THEN PRINT"S^2";
5480 IF I2=4 THEN PRINT"(S^2+";WF;")^2";
5490 FOR L=1 TO K
5500 PRINT"(S^4+";A(2,L);"S^2+";A(0,L);")";
5510 NEXT L
5520 PRINT""
5530 RETURN
5570 IF I1=1 AND I2=1 THEN GOSUB 5710 
5580 IF I1=1 AND I2=2 THEN GOSUB 5790 
5590 IF I1=1 AND I2=3 THEN GOSUB 5870 
5600 IF I1=1 AND I2=4 THEN GOSUB 5980 
5610 RETURN
5650 B(1,L)=B(1,L)*WP*(E^(-1/N))
5660 B(0,L)=(WP^2)*(E^(-2/N))
5670 RETURN
5710 IF I3=3 THEN WP=WS
5715 A(0,L)=A(0,L)*(WP^2)
5730 B(1,L)=B(1,L)*WP
5740 B(0,L)=B(0,L)*(WP^2)
5750 RETURN
5790 IF I3=3 THEN WS=WP
5795 A(0,L)=(WS^2)/A(0,L)
5810 B(1,L)=B(1,L)*WS/B(0,L)
5820 B(0,L)=(WS^2)/B(0,L)
5830 RETURN
5870 IF I3=3 THEN BF=WS-WP
5875 A(2,L)=2*WF+(A(0,L)*BF^2)
5880 A(0,L)=WF^2
5900 B(3,L)=B(1,L)*BF
5910 B(2,L)=2*WF+(B(0,L)*BF^2)
5920 B(1,L)=B(1,L)*WF*BF
5930 B(0,L)=WF^2
5940 RETURN
5980 IF I3=3 THEN BF=W2-W1
5985 A(2,L)=2*WF+(BF^2)/A(0,L)
5990 A(0,L)=WF^2
6010 B(3,L)=B(1,L)*BF/B(0,L)
6020 B(2,L)=2*WF+(BF^2)/B(0,L)
6030 B(1,L)=B(1,L)*WF*BF/B(0,L)
6040 B(0,L)=WF^2
6050 RETURN
6090 CLS
6100 PRINT"Projeto da rede ladder LCDT para a aproximacao de Butterworth:"
6110 GOSUB 8770
6120 FOR L=1 TO N
6130 LC=2*E^(1/N)*SIN((2*L-1)*PI/(2*N))
6140 IF INT(L/2)=L/2 THEN L(L)=LC ELSE C(L)=LC
6150 NEXT L
6160 R(1)=1
6170 R(2)=1
6180 GOSUB 8220 
6190 RETURN
6230 CLS
6240 PRINT"Projeto da rede ladder LCDT para a aproximacao de Chebyschev:"
6250 GOSUB 8770 
6260 KH=SQR(1+(E^-2)) 
6270 KH=((1/E)+KH)^(1/N)
6280 LA=(KH-(1/KH)) 
6290 R(1)=1
6300 C(1)=(4*SIN(PI/(2*N)))/(LA*R(1))
6310 FOR L=1 TO K
6320 L(2*L)=16*SIN((4*L-3)*PI/(2*N))*SIN((4*L-1)*PI/(2*N))
6330 L(2*L)=L(2*L)/(C(2*L-1)*(LA^2+4*(SIN((2*L-1)*PI/N))^2))
6340 C(2*L+1)=16*SIN((4*L-1)*PI/(2*N))*SIN((4*L+1)*PI/(2*N))
6350 C(2*L+1)=C(2*L+1)/(L(2*L)*(LA^2+4*(SIN(2*L*PI/N))^2))
6360 NEXT L
6370 IF K=N/2 THEN R(2)=(LA*L(N))/(4*SIN(PI/(2*N))) ELSE R(2)=(4*SIN(PI/(2*N)))/(LA*C(N))
6380 GOSUB 8220 
6390 RETURN
6430 CLS
6440 PRINT"Sintese por deslocamento de zeros:"
6450 GOSUB 8770 
6460 IF I3=3 THEN K(N+1)=H
6470 IF I3=4 THEN GOSUB 6700 
6480 FOR L=1 TO K
6490 B(2,L)=1
6500 NEXT L
6510 FOR L=1 TO 3
6520 FOR M=1 TO K+1
6530 X(L,M)=B(L-1,M) 
6540 NEXT M
6550 NEXT L
6560 IF K=N/2 THEN X(1,K+1)=1: X(2,K+1)=0: X(3,K+1)=0
6570 IF K<>N/2 THEN X(2,K+1)=1: X(3,K+1)=0
6580 GOSUB 6840 
6590 FOR L=1 TO N+1
6600 H(L)=Y(L)*H 
6610 NEXT L
6620 GOSUB 7120
6630 GOSUB 7270
6640 R(1)=1
6650 GOSUB 8220 
6660 RETURN
6700 FOR L=1 TO K
6710 X(2,L)=0
6720 X(3,L)=1
6730 NEXT L
6740 IF K<>N/2 THEN X(1,K+1)=0: X(2,K+1)=1: X(3,K+1)=0
6750 IF K=N/2 THEN X(1,K+1)=1: X(2,K+1)=0: X(3,K+1)=0
6760 GOSUB 6840 
6770 FOR L=1 TO N+1
6780 K(L)=Y(L)*H 
6790 NEXT L
6800 RETURN
6840 N3=0 
6850 N1=2 
6860 N2=2 
6870 FOR L=1 TO 3
6880 F(L)=X(L,1) 
6890 NEXT L
6900 IF N=2 THEN GOTO 7080 
6910 FOR ID=1 TO K
6920 N3=N1+N2+1
6930 FOR L=1 TO N3
6940 Y(L)=0 
6950 NEXT L
6960 FOR M=1 TO N1+1
6970 FOR L=1 TO N2+1
6980 N4=M+L-1 
6990 Y(N4)=F(M)*X(L,ID+1)+Y(N4)
7000 NEXT L
7010 NEXT M
7020 FOR L=1 TO N3
7030 F(L)=Y(L)
7040 NEXT L
7050 N1=N3-1 
7060 NEXT ID
7080 RETURN
7120 FOR L=1 TO N+1
7130 IF K=N/2 THEN GOSUB 7170 ELSE GOSUB 7210 
7140 NEXT L
7150 RETURN
7170 IF L/2<>INT(L/2) THEN ZN(L)=0: ZD(L)=H(L)+K(L)
7180 IF L/2=INT(L/2) THEN ZN(L)=H(L): ZD(L)=0
7190 RETURN
7210 IF L/2<>INT(L/2) THEN ZN(L)=H(L): ZD(L)=0
7220 IF L/2=INT(L/2) THEN ZN(L)=0: ZD(L)=H(L)+K(L) 
7230 RETURN
7270 IF I3=4 AND K=N/2 THEN K=K-1
7280 FOR L=1 TO K
7290 IF K=N/2-1 THEN GOSUB 7540 ELSE GOSUB 7640 
7300 C(2*L-1)=CD/CN
7310 FOR M=1 TO N+1
7320 ZD(M)=ZD(M)-ZN(M-1)*C(2*L-1) 
7330 F(M)=ZD(M) 
7340 NEXT M
7350 GOSUB 7940 
7360 IF K=N/2-1 THEN GOSUB 7740 ELSE GOSUB 7840 
7370 C(2*L)=CD/CN
7380 L(2*L)=1/(A(0,L)*C(2*L))
7390 FOR M=1 TO N+1
7400 ZD(M)=Y(M)
7410 ZN(M)=ZN(M)-ZD(M-1)/C(2*L)
7420 F(M+1)=ZN(M)
7430 NEXT M
7440 GOSUB 7940 
7450 FOR M=1 TO N+1
7460 ZN(M)=Y(M+1)
7470 NEXT M
7480 NEXT L
7490 IF K=N/2-1 THEN GOSUB 8090 ELSE GOSUB 8160
7500 RETURN
7540 CN=0
7550 CD=ZD(1)
7560 FOR M=3 TO N+1 STEP 2
7570 CN=((-A(0,L))^(( M-1)*.5))*ZN(M-1)+CN
7580 CD=((-A(0,L))^((M-1)*.5))*ZD(M)+CD
7590 NEXT M
7600 RETURN
7640 CN=ZN(1)
7650 CD=ZD(2)
7660 FOR M=3 TO N STEP 2
7670 CN=((- A(0,L))^((M-1)*.5))*ZN(M)+CN
7680 CD=((-A(0,L))^((M-1)*.5))*ZD(M+1) +CD
7690 NEXT M
7700 RETURN
7740 CN=ZN(2)
7750 CD=Y(1)
7760 FOR M=4 TO N STEP 2
7770 CN =((-A(0,L))^((M-2)*.5))*ZN(M)+CN
7780 CD=((-A(0,L))^((M-2)*.5))*Y(M-1)+CD
7790 NEXT M
7800 RETURN
7840 CN=ZN(1)
7850 CD=0
7860 FOR M=3 TO N STEP 2
7870 CN=((-A(0,L))^(( M-1)*.5))*ZN(M)+CN
7880 CD=((-A(0,L))^((M-1)*.5))*Y(M-1)+CD
7890 NEXT M
7900 RETURN
7940 FOR M=1 TO N+1
7950 Y(M)=0
7960 NEXT M
7970 Y(N-1)=F(N+1)
7980 IF N=3 THEN GOTO 8050
7990 FOR M=N-3 TO 1 STEP-2
8000 Y(M)=F(M+2)-A(0,L)*Y(M+2) 
8010 NEXT M
8020 Y(N)=0
8030 Y(N+1)=0
8050 RETURN
8090 L(N)=ZN(2)/ZD(1)
8100 R(2)=1
8110 C(N-1)=ZD(3)/ZN(2)
8120 RETURN
8160 C(N)=ZD(2)/ZN(1)
8170 R(2)=1
8180 RETURN
8220 IF I2=1 THEN GOSUB 8400
8230 IF I2=2 THEN GOSUB 8480
8240 IF I2=3 THEN GOSUB 8570
8250 IF I2=4 THEN GOSUB 8670
8254 IF I2>2 AND I3>2 THEN GOSUB 9360
8255 GOSUB 9180
8256 PRINT
8270 FOR L=1 TO N
8271 L$=STR$(L)
8272 L$=RIGHT$(L$,LEN(L$)-1)
8280 IF C(L)<>0 THEN PRINT"C";L$;"=";C(L)
8290 IF L(L)<>0 THEN PRINT"L";L$;"=";L(L)
8300 IF CS(L)<>0 THEN PRINT"CS";L$;"=";CS(L)
8310 IF LS(L)<>0 THEN PRINT"LS";L$;"=";LS(L)
8320 NEXT L
8330 PRINT"R1=";R(1)
8340 PRINT"R2=";R(2)
8355 GOSUB 9045
8360 RETURN
8400 FOR L=1 TO N
8410 IF I3<>3 THEN C(L)=C(L)/WP ELSE C(L)=C(L)/WS
8420 IF I3<>3 THEN L(L)=L(L)/WP ELSE L(L)=L(L)/WS
8430 NEXT L
8440 RETURN
8480 FOR L=1 TO N
8490 LC=L(L): IF I3=3 THEN WS=WP
8500 IF C(L)=0 THEN L(L)=0 ELSE L(L)=1/(WS*C(L))
8510 IF LC=0 THEN C(L)=0 ELSE C(L)=1/(WS*LC)
8520 NEXT L
8530 RETURN
8570 FOR L=1 TO N
8575 IF I3=3 THEN BF=WS-WP
8580 LS(L)=L(L)/BF: L(L)=0
8590 IF LS(L)=0 THEN CS(L)=0 ELSE CS(L)=1/(WF*LS(L))
8600 C(L)=C(L)/BF
8610 IF C(L)=0 THEN L(L)=0 ELSE L(L)=1/(WF*C(L))
8620 NEXT L
8630 RETURN
8670 FOR L=1 TO N
8675 IF I3=3 THEN BF=W2-W1
8680 CS(L)=BF*C(L)/WF
8690 C(L)=0
8700 IF CS(L)=0 THEN LS(L)=0 ELSE LS(L)=1/(WF*CS(L))
8710 IF L(L)<>0 THEN C(L)=1/(L(L)*BF): L(L)=1/(WF*C(L))
8720 NEXT L
8730 RETURN
8770 PRINT"Freq.max.=";WS
8780 PRINT"Freq.min.=";WP
8790 IF I2<3 THEN GOTO 8830   
8800 PRINT"Freq.max. da ";B1$;"=";W2
8810 PRINT"Freq.min. da ";B1$;"=";W1
8830 PRINT"Aten.min.=";AZ
8840 PRINT"Aten.max.=";AP
8850 PRINT"Ordem do filtro: n=";
8855 IF I2<3 THEN PRINT N ELSE PRINT N*2
8870 RETURN
8910 PRINT:PRINT"Opcao: ";
8930 A$=INKEY$
8940 IF A$="" THEN GOTO 8930
8945 IF A$>"Z" THEN A$=CHR$(ASC(A$)-32)
8950 PRINT A$
8960 RETURN
9000 PRINT:PRINT"Opcao invalida"
9010 GOTO 8910
9030 A$=INKEY$: IF A$="" THEN GOTO 9030
9040 RETURN
9045 NM$="":PRINT:PRINT"Salvar a rede (nome ou CR)";:INPUT NM$:IF NM$="" THEN RETURN
9050 OPEN NM$ FOR OUTPUT AS #1
9060 FOR L=1 TO N
9064 L$=STR$(L)
9065 L$=RIGHT$(L$,LEN(L$)-1)
9070 IF C(L)<>0 THEN PRINT#1,"C";L$;C(L)
9080 IF L(L)<>0 THEN PRINT#1,"L";L$;L(L)
9090 IF CS(L)<>0 THEN PRINT#1,"CS";L$;CS(L)
9100 IF LS(L)<>0 THEN PRINT#1,"LS";L$;LS(L)
9110 NEXT L
9130 PRINT#1,"R1";R(1)
9135 PRINT#1,"R2";R(2)
9150 CLOSE #1
9160 PRINT:PRINT"Arquivo ";NM$;" criado"
9170 RETURN
9180 PRINT:PRINT"Otimizar dispersao de valores? (s/n)": GOSUB 8910: IF A$="N" THEN RETURN
9186 CM=100000!: LM=100000!
9190 FOR L=1 TO N
9200 IF C(L)<>0 AND C(L)<CM THEN CM=C(L)
9210 IF CS(L)<>0 AND CS(L)<CM THEN CM=CS(L)
9220 IF L(L)<>0 AND L(L)<LM THEN LM=L(L)
9230 IF LS(L)<>0 AND LS(L)<LM THEN LM=LS(L)
9240 NEXT L
9250 X=SQR(CM/LM)
9260 FOR L=1 TO N
9270 C(L)=C(L)/X
9280 CS(L)=CS(L)/X
9290 L(L)=L(L)*X
9300 LS(L)=LS(L)*X
9320 NEXT L
9330 R(1)=R(1)*X
9340 R(2)=R(2)*X
9350 RETURN
9360 PRINT:PRINT"Converter tanques duplos? (s/n)": GOSUB 8910: IF A$="N" THEN RETURN
9370 FOR L=1 TO N
9380 IF CS(L)=0 OR C(L)=0 THEN 9440
9390 C1=C(L): L1=L(L): C2=CS(L): L2=LS(L)
9400 X=(1+C1/C2+L2/L1)^2-4*C1/C2*L2/L1
9410 Y=SQR(1-4*L2/X/L1): Z=SQR(1-4*C1/X/C2)
9420 L(L)=L1*(1-Y)/2: C(L)=C2*X*(1-Z)/2
9430 LS(L)=L1*(1+Y)/2: CS(L)=C2*X*(1+Z)/2
9440 NEXT L
9450 RETURN
9460 END
