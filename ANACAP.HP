10 ' Analise Cap ch 21/03/85
20 INTEGER A,B,C,E1,E2,E3,C1,A1,B1,A2,B2,F,F0,I,J,K,L,L0,N,N0,N1,N2,M1,M2,S,U,W,W1
30 INTEGER L1(2),L2(2),C1(2),C2(2),C(14),L(14)
40 SHORT M(1,31),A(1,31),F(31),Z(7,11)
50 DIM Z1(6,8),Z2(6,8)
60 DEF FNP$(X,Y) = X$&"%"&CHR$(X)&CHR$(Y)
70 DEF FNC(X1,X2,Y1,Y2)
80 FNC=X1*Y1-X2*Y2
90 I0=X1*Y2+X2*Y1
100 END DEF
110 X$=CHR$(27) : Y1=360/14 : F1,F2,H1,H2,E1,E2,S,W=0
120 DISP X$;"E"; : INPUT "Arquivo? ";N$
130 ASSIGN # 1 TO N$
140 READ # 1 ; N0,F0
150 PRINT "Numero de nos: ";N0
160 PRINT "Numero de fases: ";F0
170 N,N2=F0*N0
180 FOR I=0 TO N2
190 C(I),L(I)=I
200 NEXT I
210 FOR I=0 TO N0
220 FOR J=0 TO N0+F0
230 Z(I,J)=0
240 NEXT J : NEXT I
250 READ # 1 ; N$,V,A,B
260 ON POS("CXTAF",UPRC$(N$[1,1]))+1 GOSUB 280,290,360,580,660,760
270 GOTO 250
280 PRINT "Erro nos dados" : STOP
290 REM capacitor
300 PRINT "Cap. ";N$;V;"pF";A;"-";B
310 Z(A,A)=Z(A,A)+V
320 Z(B,B)=Z(B,B)+V
330 Z(A,B)=Z(A,B)-V
340 Z(B,A)=Z(B,A)-V
350 RETURN
360 REM chave
370 F=V
380 PRINT "Chave ";N$;" fase";F;A;"-";B
390 GOSUB 400 : GOSUB 510 : RETURN
400 A2,B2=0
410 IF A#0 THEN A2=A+(F-1)*N0
420 IF B#0 THEN B2=B+(F-1)*N0
430 A1=MIN(C(A2),C(B2)) : B1=MAX(C(A2),C(B2))
440 IF A1=B1 THEN 500
450 FOR I=1 TO N2
460 IF C(I)=B1 THEN C(I)=A1
470 IF C(I)>B1 THEN C(I)=C(I)-1
480 NEXT I
490 N=N-1 : RETURN
500 PRINT "Circuito proibido!" : BEEP : STOP
510 A1=MIN(L(A2),L(B2)) : B1=MAX(L(A2),L(B2))
520 IF A1=B1 THEN 500
530 FOR I=1 TO N2
540 IF L(I)=B1 THEN L(I)=A1
550 IF L(I)>B1 THEN L(I)=L(I)-1
560 NEXT I
570 RETURN
580 REM chave dupla
590 F=VAL(N$[3,3])
600 A2=A : A=V : V=A2
610 GOSUB 380
620 F=VAL(N$[2,2])
630 A2=V : V=B : B=A2
640 GOSUB 380
650 RETURN
660 REM amp op
670 PRINT "Amp op ";N$;" ";STR$(A);"-,";STR$(B);"+,";STR$(V);"s"
680 FOR F=1 TO F0
690 GOSUB 400
700 NEXT F
710 B2=0 : A=V
720 FOR F=1 TO F0
730 A2=A+(F-1)*N0 : GOSUB 510
740 NEXT F
750 RETURN
760 REM fonte
770 PRINT "Fonte ";N$;V;"V";A;"->";B
780 IF A#0 THEN 500
790 FOR F=1 TO F0
800 K=(F-1)*N0 : B1=B+K : N1=N0+F
810 FOR I=1 TO N0
820 FOR J=1 TO N0
830 IF C(J+K)=C(B1) THEN Z(I,N1)=Z(I,N1)-V*Z(I,J)
840 NEXT J : NEXT I : NEXT F
850 FOR F=1 TO F0
860 GOSUB 390
870 NEXT F
880 POP
890 N1=N+F0
900 PRINT "Ordem do sistema: ";N;"x";N1
910 INPUT "Freq. de chaveamento (Hz): ";F9
920 REM reducao
930 FOR I=0 TO N
940 FOR J=0 TO N1
950 Z1(I,J)=0
960 NEXT J : NEXT I
970 FOR F=1 TO F0
980 C1(F),L1(F)=N
990 C2(F),L2(F)=1
1000 NEXT F
1010 FOR F=1 TO F0
1020 K=(F-1)*N0
1030 FOR I=1 TO N0
1040 A=L(K+I) : A1=L(1+MOD(K+I+N0-1,N2))
1050 Z1(A,N+F)=Z1(A,N+F)+Z(I,N0+F)
1060 Z1(A1,N+F)=Z1(A1,N+F)-Z(I,N0+F)
1070 IF A#0 THEN L1(F)=MIN(L1(F),A)
1080 L2(F)=MAX(L2(F),A)
1090 IF C(I+K)#0 THEN C1(F)=MIN(C1(F),C(I+K))
1100 C2(F)=MAX(C2(F),C(I+K))
1110 FOR J=1 TO N0
1120 B=C(K+J)
1130 Z1(A,B)=Z1(A,B)+Z(I,J)
1140 Z1(A1,B)=Z1(A1,B)-Z(I,J)
1150 NEXT J : NEXT I : NEXT F
1160 FOR I=1 TO N
1170 FOR J=1 TO N1
1180 Z(I,J)=Z1(I,J)
1190 NEXT J : NEXT I
1200 PRINT X$;"E";
1210 DISP "Log ou lIn?, Hz ou R"; : INPUT "d/s? ","L,R";L$,Z$
1220 IF UPRC$(Z$)="H" THEN H=2*PI : U$="Hz" ELSE H=1 : U$="R/s"
1230 L0=UPRC$(L$)="I"
1240 REM Calculo e grafico
1250 PRINT FNP$(0,15);X$;"J";
1260 T$=STR$(S)&","&STR$(E2)&","&STR$(E1)&","&STR$(F1)&","&STR$(F2)&","&STR$(H1)&","&STR$(H2)
1270 INPUT "n,s,e,i,f,b,a: ",T$;S,E3,E1,F3,F4,H1,H2
1280 W1=C((E3-1)*N0+S)
1290 IF W1=0 THEN PRINT "V(";S;")";E3;"conhecido" : GOTO 1270
1300 IF NOT (W1=W AND E2=E3 AND F1=F3 AND F4=F2) THEN C1=-1
1310 F1=F3 : F2=F4 : E2=E3 : W=W1
1320 PRINT X$;"E";X$;"<";
1330 IF L0 THEN X=(F2-F1)/31 ELSE X=(F2/F1)^(1/31)
1340 Y=(H2-H1)/14
1350 PRINT USING "14("|"/),32("-"),"V",k,"(",k,")/Vin",k" ; S,E2,E1;
1360 F8=F1
1370 FOR C=0 TO 31
1380 IF C<=C1 THEN 1830
1390 Z=H*F8/F9/F0 : Z1=COS(Z) : Z2=SIN(Z)
1400 REM montagem
1410 FOR I=1 TO N
1420 FOR J=1 TO N1
1430 Z1(I,J)=Z(I,J)
1440 Z2(I,J)=0
1450 NEXT J : NEXT I
1460 FOR F=1 TO F0
1470 K=N+F
1480 FOR I=L1(F) TO L2(F)
1490 Z1(I,K)=FNC(Z1(I,K),Z2(I,K),Z1,Z2) : Z2(I,K)=I0
1500 FOR J=C1(F) TO C2(F)
1510 Z1(I,J)=FNC(Z1(I,J),Z2(I,J),Z1,Z2) : Z2(I,J)=I0
1520 NEXT J : NEXT I : NEXT F
1530 REM resolucao
1540 FOR I=1 TO N
1550 T1,T2=0 : U=I
1560 FOR K=I TO N
1570 IF ABS(Z1(K,I))+ABS(Z2(K,I))>ABS(T1)+ABS(T2) THEN U=K : T1=Z1(K,I) : T2=Z2(K,I)
1580 NEXT K
1590 IF I=U THEN 1640
1600 FOR K=I TO N1
1610 D=Z1(I,K) : Z1(I,K)=Z1(U,K) : Z1(U,K)=D
1620 D=Z2(I,K) : Z2(I,K)=Z2(U,K) : Z2(U,K)=D
1630 NEXT K
1640 FOR J=N1 TO I+1 STEP -1
1650 D=T1*T1+T2*T2
1660 IF ABS(D)<.0000000001 THEN PRINT "Solucao impossivel" : STOP
1670 P=(Z1(I,J)*T1+Z2(I,J)*T2)/D
1680 P2,Z2(I,J)=(Z2(I,J)*T1-Z1(I,J)*T2)/D
1690 P1,Z1(I,J)=P
1700 FOR K=1 TO N
1710 IF I=K THEN 1740
1720 Z1(K,J)=Z1(K,J)-FNC(Z1(K,I),Z2(K,I),P1,P2)
1730 Z2(K,J)=Z2(K,J)-I0
1740 NEXT K : NEXT J : NEXT I
1750 REM guardar resultados
1760 F(C)=F8
1770 FOR F=1 TO F0
1780 D=SQR(Z1(W,N+F)^2+Z2(W,N+F)^2)
1790 IF D#0 THEN M(F-1,C)=20*LOG10(D) ELSE M(F-1,C)=-500
1800 IF D#0 THEN A(F-1,C)=180/PI*ANGLE(Z1(W,N+F),Z2(W,N+F)) ELSE A(F-1,C)=0
1810 NEXT F
1820 REM Grafico
1830 A2=INT((-180-A(E1-1,C))/Y1+14.5)
1840 PRINT FNP$(C,A2);"*";
1850 M2=INT((H1-M(E1-1,C))/Y+14.5)
1860 IF C=0 THEN M1=M2
1870 IF M2>M1 THEN L=1 ELSE L=-1
1880 FOR I=M1 TO M2 STEP L
1890 IF I<0 OR I>14 THEN 1920
1900 PRINT FNP$(C,I);
1910 IF A2#I THEN PRINT " ";ELSE PRINT "ª";
1920 NEXT I
1930 M1=M2
1940 IF L0 THEN F8=F8+X ELSE F8=F8*X
1950 IF KEY$="" THEN 1970
1960 IF C<C1 THEN PUT " " ELSE C1=C : GOTO 1990
1970 NEXT C
1980 C1=31
1990 PRINT FNP$(0,15)&X$&"<Outro Gravar < >";X$;"J";
2000 I,C=0
2010 ON POS("O<>G",UPRC$(KEY$))+1 GOTO 2010,1250,2020,2030,2080
2020 C=MAX(I-1,0) : GOTO 2040
2030 C=MIN(I+1,C1)
2040 PRINT FNP$(I,14);"-";FNP$(C,14);CHR$(252);FNP$(0,15);
2050 PRINT USING "d.dde,x,3a,x,s3d.dd," dB ",s3d.d" ; F(C),U$,M(E1-1,C),A(E1-1,C);
2060 I=C
2070 GOTO 2010
2080 N$=".V"&STR$(S)&STR$(E2)&STR$(E1) : ASSIGN # 1 TO N$
2090 PRINT FNP$(0,15);"Gravando "&N$&X$&"J";
2100 FOR J=0 TO C1
2110 PRINT # 1 ; F(J),M(E1-1,J),A(E1-1,J)
2120 NEXT J
2130 GOTO 1990
